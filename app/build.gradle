apply from: '../buildsystem/dependencies.gradle'
apply from: '../buildsystem/ci.gradle'

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

if (project.hasProperty("FbsRelease2.signing")
        && new File(project.property("FbsRelease2.signing") + ".gradle").exists()) {
    apply from: project.property("FbsRelease2.signing") + ".gradle";
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId 'com.github.filipebezerra.bible.verseoftheday'
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 30
        versionName '0.3.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile libs.supportDesign
    compile libs.butterKnife
    compile(libs.retrofit) { exclude module: 'okhttp' }
    compile libs.retrofitGsonConverter
    compile libs.retrofitRxJavaAdapter
    compile libs.rxAndroid
    compile libs.okHttp
    compile libs.okHttpLoggingInterceptor
    compile libs.unbescape
    compile libs.robototextview

    testCompile unitTests.junit
}

retrolambda {
    jdk System.getenv("JAVA8_HOME")
    oldJdk System.getenv("JAVA_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

android.applicationVariants.all{ variant ->
    variant.mergeAssets.doFirst {
        File fonts = file("${projectDir}/build/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/${robototextviewVersion}/assets/fonts")
        if (fonts.exists()) {
            for (File file : fonts.listFiles()) {
                if (!file.getName().contains("RobotoSlab-Light")&&!file.getName().contains("Roboto-Thin")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
            }
        }
    }
}